allprojects {
	apply plugin: 'eclipse'
	apply plugin: 'idea'
	apply plugin: 'jacoco'

	repositories {
		jcenter()
	}
}

subprojects {
	apply plugin: 'java'
	apply plugin: 'maven-publish'

	sourceCompatibility = '1.8'

	group 'model'
	version '7.0.0'

	jacocoTestReport {
		reports {
			html.enabled true
			xml.enabled true
			csv.enabled false
		}
	}

	task sourceJar(type: Jar) {
		classifier 'sources'
		from sourceSets.main.allJava
	}

	artifacts {
		runtime sourceJar
	}
}

apply plugin: 'com.github.kt3k.coveralls'

buildscript {
	repositories {
		jcenter()
	}

	dependencies {
		classpath 'org.kt3k.gradle.plugin:coveralls-gradle-plugin:2.3.1'
	}
}

task jacocoTestReport(type: JacocoReport) {
	dependsOn subprojects.test
	sourceDirectories = files(subprojects.sourceSets.main.allSource.srcDirs)
	classDirectories = files(subprojects.sourceSets.main.output)
	executionData project(':organization-model').jacocoTestReport.executionData
	reports {
        html.enabled = true
        xml.enabled = true
        csv.enabled = false
    }
}

ext {
	slf4j_version = '1.7.10'
	javaee_version = '7.0'
	guice_version = '4.0-beta5'
	junit_version = '4.12'
	mockito_version = '2.0.5-beta'
	jmockit_version = '1.16'
}

task wrapper(type: Wrapper) {
	gradleVersion = '2.3'
}
