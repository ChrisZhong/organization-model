buildscript {
    repositories {
        jcenter()
    }

    dependencies {
        classpath 'io.spring.gradle:dependency-management-plugin:0.6.0.RELEASE'
        classpath 'org.kt3k.gradle.plugin:coveralls-gradle-plugin:2.6.3'
    }
}

allprojects {
    apply plugin: 'eclipse'
    apply plugin: 'idea'
    apply plugin: 'jacoco'
    apply plugin: 'com.github.kt3k.coveralls'

    repositories {
        jcenter()
    }
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'maven-publish'
    apply plugin: 'io.spring.dependency-management'

    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8

    group 'io.github.runtimemodels'
    version '7.0.0'

    dependencyManagement {
        dependencies {
            dependency 'javax:javaee-api:7.0'
            dependency 'org.jmockit:jmockit:1.25'
            dependency 'org.projectlombok:lombok:1.16.8'
        }
        imports {
            mavenBom 'io.spring.platform:platform-bom:2.0.5.RELEASE'
            mavenBom 'com.google.inject:guice-bom:4.0'
        }
    }

    jacocoTestReport {
        dependsOn check
        reports {
            html {
                enabled true
            }
            xml {
                enabled true
            }
            csv {
                enabled false
            }
        }
    }

    task sourceJar(type: Jar) {
        classifier 'sources'
        from sourceSets.main.allJava
    }

    artifacts {
        runtime sourceJar
    }

    publishing {
        publications {
            maven(MavenPublication) {
                from components.java
                artifact sourceJar
            }
        }
    }
}

coveralls {
    sourceDirs = subprojects.sourceSets.main.java.srcDirs.flatten()
    jacocoReportPath = "${buildDir}/reports/jacoco/jacocoReports/jacocoReports.xml"
}

task jacocoTestReport(type: JacocoReport) {
    dependsOn subprojects.jacocoTestReport

    sourceDirectories = files(subprojects.sourceSets.main.java.srcDirs)
    classDirectories = files(subprojects.sourceSets.main.output.classesDir)
    executionData = files(files(subprojects.jacocoTestReport.executionData).findAll { it.exists() })

    reports {
        html {
            enabled true
        }
        xml {
            enabled true
        }
        csv {
            enabled false
        }
    }
}

tasks.coveralls {
    dependsOn jacocoTestReport
}

task wrapper(type: Wrapper) {
    gradleVersion = '2.14'
    distributionUrl = "https://services.gradle.org/distributions/gradle-${gradleVersion}-all.zip"
}
